?message : (literal?|format)+
literal : LITERAL | BRA | CES | VBAR |PERCENT
PERCENT : "%%"
VBAR.-1 : "|" 
BRA : "{"
CES : "}"
LITERAL.1 : /[^%{}\|]+/

?format : placeholder
         | s_format
         | select_format
         | enum_select_format
         | plural_format
         | ordinal_format
         | human_format
         | objcclass_format
         | objcinstance_format
         | q_format
         | diff_format
         | sub_format
         | quoted_format

ARG_NO.1 : /\d/
NUM : /\d/+
placeholder : "%" ARG_NO
s_format : "%s" ARG_NO
ID : /[a-zA-Z]\w*/
select_format : "%select{" message ("|" message)+ "}"  ARG_NO
enum_select_format : "%enum_select<" ID ">{" enum_selection ("|" enum_selection)+ "}"  ARG_NO
plural_choice : (plural_cond ":" message)| (":" message)
range : "[" NUM "," NUM "]"
plural_cond : NUM
            | range
            | "%" NUM "=" (NUM | range)
plural_format : "%plural{" plural_choice ("|" plural_choice)+ "}" ARG_NO
ordinal_format : "%ordinal" ARG_NO
human_format : "%human" ARG_NO
objcclass_format : "%objcclass" ARG_NO
objcinstance_format : "%objcinstance"  ARG_NO
q_format : "%q" ARG_NO
diff_format : "%diff{" message "|" message "}" ARG_NO "," ARG_NO
sub_format : "%sub{" ID "}" ARG_NO ("," ARG_NO)*
quoted_format : "%quoted" ARG_NO
enum_selection : "%" ID "{" message "}"
